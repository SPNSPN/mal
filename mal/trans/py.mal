(load "mal/interpreter.mal")

(define trans::py::indent
  (lambda (line)
	(sprint "\t" line)))

(define trans::py::sys-var "_sys_var")

(! let ((counter -1))
   (define trans::py::gen-lambda-name
	 (lambda () (do (setq counter (+ counter 1))
				  	(sprint "_sys_lambda_" counter)))))

(! let ((counter -1))
   (define trans::py::gen-do-name
	 (lambda () (do (setq counter (+ counter 1))
				  	(sprint "_sys_do_" counter)))))

(define trans::py::env nil)

(define trans::py::regist
  (lambda (sym body) `(! push trans::py::env (cons ',sym (lambda (args env) ,body)))))

(define trans::py::regist-mid
  (lambda (sym op zeros)
	`(! push  trans::py::env
		(cons ',sym
			  (lambda (args env)
				(! let ((vals (map trans::py::encode
								   (! cond 
									  ((= 0 (length args)) (list ,zeros)
									   (= 1 (length args)) (cons ,zeros args)
									   t args)))))
				   (append (apply append (map (! fn (tail-drop $0 1)) vals))
						   (list (sprint "(" (join (map back vals) ,op)
										 ")")))))))))

; TODO 末尾呼出最適化
(! trans::py::regist if (! let ((pred (trans::py::encode (car args)))
								(then (trans::py::encode (nth args 1)))
								(else (trans::py::encode (nth args 2))))
						   (append (tail-drop pred 1)
								   (tail-drop then 1)
								   (tail-drop else 1)
								   (list (sprint "(" (back then) " if " (back pred)
												 " else " (back else) ")")))))

(! trans::py::regist lambda (! let ((lambda-name (trans::py::gen-lambda-name))
									(lambda-body (trans::py::encode (nth args 1))))
				   (append (list (sprint "def " lambda-name " ("
										 (join (car args) ", ") "):"))
						   (map trans::py::indent (tail-drop lambda-body 1))
						   (list (trans::py::indent
								   (sprint "return " (back lambda-body)))
								 lambda-name))))

(! trans::py::regist define (! let ((sym (car args))
									(val (trans::py::encode (nth args 1))))
							   (append (tail-drop val 1)
									   (list (sprint "global " sym)
											 (sprint sym " = " (back val)))
									   (trans::py::encode `(quote ,sym)))))
(! trans::py::regist setq (! let ((sym (car args))
								  (val (trans::py::encode (nth args 1))))
							 (append (tail-drop val 1)
									 (list (sprint sym " = " (back val))
										   (string sym)))))
(! trans::py::regist quote (list (sprint "car(lread(\"" (car args) "\"))")))
(! trans::py::regist do (! let ((do-name (trans::py::gen-do-name))
								(do-body (apply append (map trans::py::encode args))))
						   (append (list (sprint "def " do-name " ():"))
								   (map trans::py::indent (tail-drop do-body 1))
								   (list (trans::py::indent
										   (sprint "return "
												   (back (if do-body do-body
													 (trans::py::encode nil)))))
										 (sprint do-name "()")))))
(! trans::py::regist-mid and " and " t)
(! trans::py::regist-mid or " or " nil)
(! trans::py::regist quasiquote (trans::py::encode
								  (mal::expand-quasiquote-proc (car args) env)))
(! trans::py::regist environment (list "genv"))
(! trans::py::regist ! (trans::py::encode
						 (mal::expand-syntax-proc
						   (car args) (cdr args) env)))
(! trans::py::regist catch (! let ((ebody (trans::py::encode (car args))))
							  (append (list "try:")
									  (map trans::py::indent
										   (trans::py::encode (nth args 1)))
									  (list "except Erro as _sys_erro:")
									  (map trans::py::indent
										   (append (tail-drop ebody 1)
												   (list (sprint (back ebody)
																 "(_sys_erro.id, _sys_erro.message)"))))
									  (list "except Exception as _sys_erro:")
									  (map trans::py::indent
										   (append (tail-drop ebody 1)
												   (list (sprint (back ebody)
																 "(-1, _sys_erro.__str__())")))))))

(! trans::py::regist trans::py::block
   (apply append (map trans::py::encode args)))

(! trans::py::regist list
   (! let ((vals (map trans::py::encode args)))
	  (append (apply append (map (! fn (tail-drop $0 1)) vals))
			  (list (sprint "l(" (join (map back vals) ", ") ")")))))

(! trans::py::regist-mid + " + " 0)
(! trans::py::regist-mid - " - " 0)
(! trans::py::regist-mid * " * " 1)
(! trans::py::regist-mid / " / " 1)

(define trans::py::replace-define
  (! letrec ((pick-define
			   (lambda (rexpr)
				 (if (atom rexpr)
				   nil
				   (if (eq 'define (car rexpr))
					 (list rexpr)
					 (apply append (map pick-define rexpr)))))))
	 (lambda (expr)
	   (append '(trans::py::block)
			   (map (! fn `(setq ,(nth $0 1) nil)) (pick-define expr))
			   (list expr)))))

(define trans::py::encode
  (! let ((var-counter 0))
	 (lambda (expr)
	   (! case (type expr)
		  ('<nil> (list "nil")
		   '<inum> (list (sprint expr))
		   '<fnum> (list (sprint expr))
		   '<strn> (list (sprint "\"" expr "\""))
		   '<symb> (list (sprint expr))
		   '<cons> (! aif (assocdr trans::py::env (car expr))
					  (it (cdr expr) (environment))
					  ; TODO 末尾呼出最適化
					  (! let ((proc (trans::py::encode (car expr)))
							  (args (map trans::py::encode (cdr expr))))
						 (append (tail-drop proc 1)
								 (apply append (map (! fn (tail-drop $0 1)) args))
								 (list (sprint (back proc) "("
											   (join (map back args) ", ") ")"))))))))))

(define trans::py::encodetop
  (lambda (expr)
	(! let ((lines (trans::py::encode expr)))
	   (append (tail-drop lines 1)
			   (list (sprint "global " trans::py::sys-var)
					 (sprint trans::py::sys-var " = " (back lines)))))))

